Quintiq file version 2.0
#parent: #root
Method Test013_Span_ScopeSpanningDownstream (Schedule schedule)
{
  Description: 'tests the span calculation\'s "spanning downstream" requirement.'
  TextBody:
  [*
    // mbr23 Jan-10-2020 (created)
    
    // start scenario for test is empty schedule:
    schedule.UnplanAll();
    
    scope := construct( LibOpt_ScopeElements );
    downstreams_planned := construct( LibOpt_ScopeElements );
    // Select operations
    operations := this.GetMethodOperationsInScope( this.GetCurrentMethodShortName(), schedule );
    traverse( operations, Elements, op )
    {
      scope.Add( op.GetOrCreateScopeElement() );
      // and plan one of its most downstream operations:
      most_downstream := op;
      more_downstream := select( most_downstream, ToOperationDependency.ToOperation, o, true );
      while( not isnull( more_downstream ) )
      {
        most_downstream := more_downstream;
        more_downstream := select( most_downstream, ToOperationDependency.ToOperation, o, true );
      }
      if( most_downstream <> op )
      {
        most_downstream.Plan();
        downstreams_planned.Add( most_downstream.GetOrCreateScopeElement() );
      }
    }
    
    run := UTF_Optimizer_Regression_Test_OptimizerSequencing::CreateSelectorScopeOnlyRun( UTF_Optimizer_Regression_Test_OptimizerSequencing::Optimizer( schedule ),
                                                                                          scope,
                                                                                          null( SettingsSequencingNeighborhoodCreatorForSpanCreator, owning ) );
    selector := selectobject( run, Component, c, true );
    
    // assert that span contains `upstreams_planned`: 
    run.Start() ->| downstreams_planned -> UTF_Optimizer_Regression_Test_OptimizerSequencing::AssertOutputScopeContains( this.Run(), selector )->Exception()->(exception) {
      this.Run().RegisterFailure( "post-optimization assert failed:\n" + exception.Message() );
    };
  *]
}
