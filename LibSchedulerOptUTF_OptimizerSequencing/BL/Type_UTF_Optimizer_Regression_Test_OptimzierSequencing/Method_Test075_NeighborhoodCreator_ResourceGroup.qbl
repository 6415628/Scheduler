Quintiq file version 2.0
#parent: #root
Method Test075_NeighborhoodCreator_ResourceGroup (Schedule schedule)
{
  Description: "Tests the NeighborhoodCreatorDistanceMetric's 'ResourceGroup' dimension"
  TextBody:
  [*
    // mbr23 Feb-17-2020 (created)
    
    anchor := minselect( schedule, Operation_SC, op, true, Real::Random() ); // select any anchor
    neighborhood_size_operations := 15;
    distancemetric := construct( SettingsSequencingDistanceMetricForSequencing ); 
    // Set all values to 0
    distancemetric.UTF_SetAllToZero();
    distancemetric.ClosenessByResourceGroup( 1.0 ); // enable the "resource group" dimension
    distancemetric.ClosenessByAllowedOn( 1.0 ); // "resource group" won't link operations if no other dimension that links operations to resources resources is selected as well
    
    selector := this.Run_Test07x_NeighborhoodCreator( schedule, anchor, neighborhood_size_operations, distancemetric );
    
    minimum_output := construct( LibOpt_ScopeElements );
    minimum_output.Add( anchor.GetOrCreateScopeElement() );
    
    maximum_output := construct( LibOpt_ScopeElements );
    maximum_output.Add( anchor.GetOrCreateScopeElement() );
    traverse( anchor, AllowedOperationOnResourceHard.Resource_SC.ResourceGroup_SC.Resource_SC.AsAllowedResourceHard.Operation_SC, op )
    {
      maximum_output.Add( op.GetOrCreateScopeElement() );
    }
    traverse( anchor, AllowedOperationOnResourceHard.Resource_SC.ResourceGroup_SC.Resource_SC, r )
    {
      maximum_output.Add( r.GetOrCreateScopeElement() );
    }
    
    
    // assert that output is between `minimum_output` and `maximum_output`:
    selector.Run().Start() ->| this->( o ) {
      UTF_Optimizer_Regression_Test_OptimizerSequencing::AssertOutputScopeContains( minimum_output, this.Run(), selector );
      UTF_Optimizer_Regression_Test_OptimizerSequencing::AssertOutputScopeIsSubsetOf( maximum_output, this.Run(), selector );
    }->Exception()->(exception) {
      this.Run().RegisterFailure( "post-optimization assert failed:\n" + exception.Message() );
    };
  *]
}
