Quintiq file version 2.0
#parent: #root
Method TestKPIOperationsDueNotPlanned_Setup (Schedule schedule)
{
  Description: 'Test KPI OperationsDueNotPlanned - setup'
  TextBody:
  [*
    isvariantfound := false;
    isdummy := false;
    hasfeedback := false;
    hasfinished := false;
    iscpplandue := false;
    isontime := false;
    isplanned := false;
    kpioperationsduenotplanned := 0.0;
    
    this.GetTestVariantForKPIOperationsDueNotPlanned( isvariantfound,
                                                      isdummy,
                                                      hasfeedback,
                                                      hasfinished,
                                                      iscpplandue,
                                                      isontime,
                                                      isplanned,
                                                      kpioperationsduenotplanned );
    
    if( isvariantfound )
    {  
      // Create resource
      resource := UTFMockObjectFactory::CreateSingleResource( schedule, 'Resource' );
      
      duedate := schedule.Time() + Duration::Days( 10 );
      
      // Create 5 operations, 4 operation not based on variant
      for( i := 1; i<=4; i++ )
      {
        // Create operation
        order := UTFMockObjectFactory::CreateOrder( schedule, 'Order ' + [String]i );
        planUnit := UTFMockObjectFactory::CreatePlanUnit( order, 'Plan Unit ' + [String]i , 100.0 );
        operation := UTFMockObjectFactory::CreateOperation( planUnit );
    
        // update UTFOperation attributes
        this.SetUTFOperationAttributesForKPI( operation, resource, duedate,
                                              false,  //isdummy
                                              false,  //hasfeedback,
                                              false,  //hasfinished
                                              false ); //isplanned
        this.SetUTFOperationAttributesForKPI_OperationsDueNotPlanned( operation,
                                                                      true,   //iscpplandue
                                                                      false ); //isontime
      }
      
      // Create operation5 based on variant
      order5 := UTFMockObjectFactory::CreateOrder( schedule, 'Order 5' );
      planUnit5 := UTFMockObjectFactory::CreatePlanUnit( order5, 'Plan Unit 5', 100.0 );
      operation5 := UTFMockObjectFactory::CreateOperation( planUnit5 );
      
      // update UTFOperation attributes
      this.SetUTFOperationAttributesForKPI( operation5, resource, duedate,
                                            isdummy,
                                            hasfeedback,
                                            hasfinished,
                                            isplanned );
      this.SetUTFOperationAttributesForKPI_OperationsDueNotPlanned( operation5, iscpplandue, isontime );
    }
  *]
}
