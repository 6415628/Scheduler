Quintiq file version 2.0
#parent: #root
Method SynchronizeCalendars (LibCal_ImportExport importExport_i) as owning LibCal_Calendars
{
  Description: 'Synchronize the imported StagingCalendars.'
  TextBody:
  [*
    synchronizedCalendars := construct( LibCal_Calendars );
    
    LibCal_Util::Info( "SynchronizeCalendars: #StagingCalendars = " + [String]this.StagingCalendar( relsize ) );
    
    traverse( this, StagingCalendar, stagCal )
    {
      calendar := null( LibCal_Calendar );
    
      // Synchronize the calendar if there are no errors.
      if( stagCal.CanSynchronize() )
      {
        // Find the calendar, create a new one if necessary.
        calendar := importExport_i.FindCreateCalendar( stagCal );
        
        if( not isnull( calendar ) )
        {
          calendar.Synchronize( stagCal );
        }
      }
      else
      {
        // Still find the calendar, so the attempt to synchronize the calendar can be registered,
        // in order to prevent the calendar from being deleted.
        calendar := importExport_i.CalendarRegistry().FindCalendar( stagCal.CalendarType(), 
                                                                    stagCal.StagingCalendarID() );    
      }
      
      if( not isnull( calendar ) )
      {
        synchronizedCalendars.Add( calendar );
      }
    }
    
    // The Windows of the calendars can be have been changed during the synchronization.
    // Make sure that the Start and End of the calendars is recalculated before synchronizing the events and subscriptions.
    Transaction::Transaction().Propagate();
    
    // Deletion of calendars is postponed until the end of the synchronization of all data.
    return &synchronizedCalendars;
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
