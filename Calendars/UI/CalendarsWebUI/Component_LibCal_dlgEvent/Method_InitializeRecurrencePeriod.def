Quintiq file version 2.0
#parent: #root
Method InitializeRecurrencePeriod (shadow[LibCal_RecurrencePeriod] sRecurrencePeriod_i, Date startDate_i) id:Method_LibCal_dlgEventParticipation_InitializeRecurrencePeriod
{
  #keys: '[131092.0.2141066155]'
  Body:
  [*
    // First initialize everything as new...
    this.InitializeRecurrencePeriodAsNew( startDate_i );
    
    // ... then take over the existing period (if applicable)
    if( not isnull( sRecurrencePeriod_i ) )
    {
      // Use the UTC-timezone here in order to keep the same date, don't correct for a specific timezone.
      timezone := TimeZone::UTCTimeZone();
      dtStartOfPeriod.DateTime( sRecurrencePeriod_i.StartDate().DateTime( timezone ) );
      dhStartOfPeriod.Data    ( dtStartOfPeriod.DateTime() );
     
      // NoEnd
      if( sRecurrencePeriod_i.istype( shadow[LibCal_RecurrencePeriodWithoutEnd] ) )
      {
        // Is already initialized
      }
    
      else if( sRecurrencePeriod_i.istype( shadow[LibCal_RecurrencePeriodWithEndDate] ) )
      {
        period := sRecurrencePeriod_i.astype( shadow[LibCal_RecurrencePeriodWithEndDate] );
    
        ckbEndOfPeriod.Checked( true );
        dtEndOfPeriod.DateTime( period.EndDate().DateTime( timezone ) );
      }
    }
    
    // Workaround for the fact that setting ckbEndOfPeriod.Checked() is not noticed during the first execution
    // of response dtStartOfPeriod.OnChanged(). See method SetEndOfPeriod() of the dialog for more information.
    lblWorkaround.Text( "initialized" );
    
    this.SetEndOfPeriod();
  *]
}
