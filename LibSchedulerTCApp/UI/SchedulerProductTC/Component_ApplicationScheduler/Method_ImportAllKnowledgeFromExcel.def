Quintiq file version 2.0
#parent: #root
Method ImportAllKnowledgeFromExcel (String scenarioCategory, String scenarioName) id:Method_ApplicationScheduler_ImportAllKnowledgeFromExcel
{
  #keys: '[141634.0.894273920]'
  Body:
  [*
    // Import all knowledge bases from excel
    
    componentmds := ComponentMDS::Create( Application );
    componentMDSKind := select( componentmds, ComponentMDSKinds, kind, kind.Kind() = typeof( Schedule ).Name() );
    mdsinstance := select( componentMDSKind, ComponentMDSInstances, m,
                             m.IsLoaded(),
                             m.MDSID().Equals( Schedule.MDSSchedule().MDSID() ) );
    
    editor := CKBEditor::Create();
    mdshandle := mdsinstance.GetMDSHandle();
    kbinfos     := editor.GetMDSKBs( mdshandle );
    
    // Get Data directory
    folderPath := Schedule.GetDataFolderPath() + '/';
    
    traverse( kbinfos, Elements, kbInfo, kbInfo.KBFullID().KBID().Name() <> Constants::GetCalendarsKnowledgeBaseName()
                                         and kbInfo.KBFullID().KBID().Name() <> Constants::GetUTFKnowledgeBaseName() )
    {                                  
      fileName := kbInfo.KBFullID().KBID().Name();
      filePath := folderPath.Concat( fileName ).Concat( Constants::Excel_XLSX() );
      
      data := Schedule.GetServerFileData( filePath );
      if( data.Size() > 0 )
      {
        ckbhandle := editor.LoadMDSKB( mdsinstance.GetMDSHandle(), kbInfo.KBFullID().KBID() );
        
        templateckb := CKBEditor::Create().GetTemplateFromKB( ckbhandle.CKB() );
        predefinedtablegroup := templateckb.CKB().GetTables();
         
        tablegrouphandle := XLS::LoadTableGroupFromBinaryData( data.AsBinaryData(), predefinedtablegroup, true, true );
      
        ckbhandle.CKB().OverwriteTables( tablegrouphandle );
        ckbhandle.CKB().SetTables( &tablegrouphandle, true );
        ckbhandle.CKB().SetDirty();
        ckbhandle.CKB().ValidateAll();
      
        editor.LockApplySaveUnlock( mdshandle, ckbhandle, Translations::SC_ImportKTFromExcel( scenarioCategory, scenarioName ), false);
        ckbhandle.CKB().ResetDirty();
      }
    }
  *]
}
