Quintiq file version 2.0
#parent: #root
Method Test060_BreakpointOnRollbackTriggersOnRollbackMultiComponentInOneTransaction (LibOpt_TestDataset dataset)
{
  Description: 'Test that the breakpoint on rollback triggers on a rollback when previous component + init + handle result is in one transaction.'
  TextBody:
  [*
    opt := this.Optimizer( dataset );
    run := opt.CreateRun();
    
    // Script of the rollback KPI
    kpi_script := opt.RollbackKPIScripted( 1 );
    kpi_script.Enqueue( 0.0 );
    kpi_script.Enqueue( 100.0 );
    
    // Create tree
    component := opt.ComponentCounter( run, 'Counter' );
    
    suboptimizer := opt.MPSuboptimizer( run, 'MP', LibOpt_MyRunContextForUTF::Create( run ), & kpi_script );
    suboptimizer.InOneTransaction( true );
    
    component.To( suboptimizer ).AsOneTransaction( false );
    
    // Set breakpoints
    component_position := select( suboptimizer, ComponentPosition, bp, bp.Name() = LibOpt_Suboptimizer::ComponentPosition_Rollback() );
    component_position.SetBreakpoint();
    
    component_position := select( component, ComponentPosition, bp, bp.Name() = LibOpt_Component::ComponentPosition_Error() );
    component_position.SetBreakpoint();
    
    // Test
    this.NoException( run.Start() );
    test := this->WaitForBreakpoint( run, this.Run() )->|void->()
    {
      if( this.Run().AssertTrue( run.BreakpointEvent( relsize ) = 1, 'Breakpoint triggered' ) )
      {
        this.Run().AssertEqual( run.BreakpointEvent().Breakpoint().ComponentName(), suboptimizer.Name(), 'Wrong component' );
      }
    }
    ->|run->Stop();
    
    this.NoException( test );
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
