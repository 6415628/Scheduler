Quintiq file version 2.0
#parent: #root
Method Test_081_RunCleanupMaxNrOfRunsWithDisable (LibOpt_TestDataset dataset)
{
  Description: 'Test if the auto cleanup with max number of runs works. Note that half of the runs have auto cleanup disabled.'
  TextBody:
  [*
    opt := this.Optimizer( dataset );
    opt.IsAutoCleanupRunsOnNrOfRuns( false );
    
    // Remove all existing runs
    opt.Run( relflush );
    
    for( i := 0; i < 20; i++ )
    {
      run := opt.CreateRun();
      run.AutoCleanupEnabled( i mod 2 = 1 );
      run.FinishedOn( DateTime::Now() - Duration::Hours( i + 0.5 ) );
    }
    
    // Add some running runs for distraction.
    // These should not even be taken into account by the auto cleanup
    for( i := 0; i < 7; i++ )
    {
      run := opt.CreateRun();
      run.StartedOn( DateTime::Now() );
    }
    
    opt.MaxNrOfRuns( 10 );
    opt.IsAutoCleanupRunsOnNrOfRuns( true );
    
    // Propagate to make sure that the status of the run is correct.
    Transaction::Transaction().Propagate();
    
    cleanup := opt.GetRunsForCleanup( 0 );
    // Expect 20 - 10. Half of them are protected, so these will not be deleted.
    this.Run().AssertEqual( 5, cleanup.Size(), 'Cleanup size' );
    
    cleanup := opt.GetRunsForCleanup( 2 );
    // Expect 20 + 2 - 10.  Half of them are protected, so these will not be deleted.
    this.Run().AssertEqual( 6, cleanup.Size(), 'Cleanup size 2' );
    this.Run().AssertEqual( 27, opt.Run( relsize ), 'Nothing changed' );
    
    opt.CreateRun();
    this.Run().AssertEqual( 15 + 7, opt.Run( relsize ), 'Nr of runs after auto cleanup' );
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
