Quintiq file version 2.0
#parent: #root
Method Test_082_RunCleanupMaxAge (LibOpt_TestDataset dataset)
{
  Description: 'Test if the auto cleanup with max age works'
  TextBody:
  [*
    opt := this.Optimizer( dataset );
    opt.IsAutoCleanupRunsOnRunAge( false );
    
    // Remove all existing runs
    opt.Run( relflush );
    
    for( i := 0; i < 20; i++ )
    {
      run := opt.CreateRun();
      run.FinishedOn( DateTime::Now() - Duration::Hours( i + 0.5 ) );
    }
    
    // Add some running runs for distraction.
    // These should not even be taken into account by the auto cleanup
    for( i := 0; i < 7; i++ )
    {
      run := opt.CreateRun();
      run.StartedOn( DateTime::Now() );
    }
    
    opt.MaxRunAge( Duration::Hours( 8 ) );
    opt.IsAutoCleanupRunsOnRunAge( true );
    
    // Propagate to make sure that the status of the run is correct.
    Transaction::Transaction().Propagate();
    
    cleanup := opt.GetRunsForCleanup( 0 );
    // Expect 8 to be inside the age limit
    this.Run().AssertEqual( 20 - 8, cleanup.Size(), 'Cleanup size' );
    
    cleanup := opt.GetRunsForCleanup( 2 );
    // Expect 8 to be inside the age limit
    this.Run().AssertEqual( 20 - 8, cleanup.Size(), 'Cleanup size 2' );
    
    this.Run().AssertEqual( 20 + 7, opt.Run( relsize ), 'Nothing changed' );
    
    opt.CreateRun();
    this.Run().AssertEqual( 8 + 1 + 7, opt.Run( relsize ), 'Nr of runs after auto cleanup' );
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
