Quintiq file version 2.0
#parent: #root
Method Test020_StatisticError (LibOpt_TestDataset dataset)
{
  Description: 'Test that the `LibOpt_StatisticError` creates issues for every `LibOpt_SnapshotError`.'
  TextBody:
  [*
    opt := this.Optimizer( dataset );
    run := opt.CreateRun();
    
    iterator := opt.Iterator( run, 'Iterator' );
    iterator.SetMaxIterations( 100 );
    component1 := opt.ComponentParentReflection( run, 'Error 1',
                                          method( LibOpt_TestUnit_Statistic, M020_Operation_ProbabilityError,
                                                  LibOpt_ComponentParent, LibOpt_Task ) );
    component2 := opt.ComponentParentReflection( run, 'Error 2',
                                          method( LibOpt_TestUnit_Statistic, M020_Operation_ProbabilityError,
                                                  LibOpt_ComponentParent, LibOpt_Task ) );
    
    iterator.To( component1 );
    component1.To( component2 );
    
    this.NoException( run.Start()->|void->( )
    {
      statistics := selectset( run, Statistic.astype( LibOpt_StatisticError ), statistic, true );
      
      this.Run().AssertEqual( 4, statistics.Size(), 'Incorrect amount of error statistics' );
      
      nr_errors := counter( run, Snapshot.astype( LibOpt_SnapshotError ), snapshot, true );
      this.Run().AssertGreaterOrEqual( 20, nr_errors, 'Unexpected low number of errors' );
      
      // Check that for every snapshot there is an issue associated
      traverse( run, Snapshot.astype( LibOpt_SnapshotError ), snapshot )
      {
        component := snapshot.GetComponent();
        issues := counter( snapshot.IterationPart(), SnapshotComponent.Issue, issue,
                           issue.Statistic().Component() = component
                           and guard( issue.Statistic().astype( LibOpt_StatisticError ).LogEntryDetails() = snapshot.Details(), false ) );
        this.Run().AssertEqual( 1, issues, 'Nr of issues for this snapshot is incorrect' );
      }
    } );
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
