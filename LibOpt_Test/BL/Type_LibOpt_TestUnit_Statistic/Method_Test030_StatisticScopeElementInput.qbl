Quintiq file version 2.0
#parent: #root
Method Test030_StatisticScopeElementInput (LibOpt_TestDataset dataset)
{
  TextBody:
  [*
    opt := this.Optimizer( dataset );
    run := opt.CreateRun();
    run.DebugScope( true );
    
    iterator := opt.Iterator( run, 'Iterator' );
    iterator.SetMaxIterations( 100 );
    
    selector := opt.ComponentParentReflection( run, 'Selector',
                                               method( LibOpt_TestUnit_Statistic, M030_Operation_SelectorProbability,
                                                       LibOpt_ComponentParent, LibOpt_Task ) );
    
    context := LibOpt_MyRunContextForUTF::Create( run );
    suboptimizer := opt.MPSuboptimizer( run, 'Suboptimizer', context );
    
    iterator.To( selector );
    selector.To( suboptimizer );
    
    never := LibOpt_ScopeElementProbability::Create( dataset, 'Never', 0.0 );
    always := LibOpt_ScopeElementProbability::Create( dataset, 'Always', 1.0 );
    scope := LibOpt_Scope::Create( run );
    scope.Add( never );
    scope.Add( always );
    for( i := 1; i <= 100; i++ )
    {
      scope.Add( LibOpt_ScopeElementProbability::Create( dataset, '50% - #' + [String] i, 0.5 ) );
    }
    
    run.SetStartScope( scope );
    run.ConfigureForOneTransaction();
    
    this.NoException( run.Start()->|void->( )
    {
      statistics := selectset( run, Statistic.astype( LibOpt_StatisticScopeElementInput ), statistic, true );
      
      if( this.Run().AssertEqual( 1, statistics.Size(), 'Incorrect amount of statistics' ) )
      {
        statistic := statistics.First();
        this.Run().AssertGreaterOrEqual( 2, statistic.Issue( relsize ), 'Unexpected amount of issues' );
        
        this.Run().AssertTrue( exists( never, SuboptimizerScopeElement.Issue, issue, issue.Statistic() = statistic ), 'Never has no issue' );
        this.Run().AssertTrue( exists( always, SuboptimizerScopeElement.Issue, issue, issue.Statistic() = statistic ), 'Always has no issue' );
      }
    } );
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
