Quintiq file version 2.0
#parent: #root
StaticMethod RenameAfterRelation (Type type, NamedValueTree nvt, NamedValue nv)
{
  Description:
  [*
    Make sure the branches in the named value tree that represent an owning relation are called after the relation name.
    Default is the type name of the owned object.
  *]
  TextBody:
  [*
    // mbr23 Apr-24-2020 (created)
    // copied from member method in this type from LibOpt, because it is de facto static.
    
    traverse( nv.Children(), Elements, child )
    {
      if( child.GetValue().istype( Key ) )
      {
        rel := select( LibOpt_Utility::GetTypeAncestors( type ), Elements.MdlRelSideElements.astype( MdlModeledRelSideElement ), rel,
                       rel.OtherSide().Type().Name() = child.Name()
                       and guard( rel.RelationSide().OwningSideBool(), false ) );
        if( not isnull( rel ) )
        {
          if( rel.Name() <> child.Name() )
          {
            new_child := nv.AddChild( nvt.GetHandle( rel.Name() ) );
            new_child.SetValue( child.GetValue() );
            child.Move( new_child, true );
            child.Delete( false );
            child := new_child;
          }
          NVTPortable::RenameAfterRelation( rel.OtherSide().Type(), nvt, child );
        }
      }
    }
  *]
}
