Quintiq file version 2.0
#parent: #root
Method Add (LibOpt_ScopeThin scope) as LibOpt_ScopeShared
{
  Description:
  [*
    Adds the given `LibOpt_ScopeThin` to the `LibOpt_ScopeShared`. This either happens by actually adding it to the `LibOpt_ScopeShared`, or creating a new one and removing this one.
    Therefore we also return a `LibOpt_ScopeShared`; this is the (new) `LibOpt_ScopeShared` that will be used.
  *]
  TextBody:
  [*
    result := this.FindSharedWith( scope );
    if( not isnull( result ) )
    {
      traverse( this, ScopeElement, se )
      {
        result.ScopeElement( relinsert, se );
      }
      this.Delete();
    }
    else
    {
      result := this.AddUnsafe( scope );
    }
    
    return result;
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
