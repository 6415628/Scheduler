Quintiq file version 2.0
#parent: #root
Method POAAddConstraintLatestEnd (SuboptimizerMRSPOA_POAOneResourceGroup poa, LibOpt_Task task)
{
  Description: 'Add the constraint which prevent poa to end the operation too late and push the operation on the other resources'
  TextBody:
  [*
    // stn4 Mar-18-2016 (created)
    
    debuginfo( 'Adding constraint latest end...' );
    
    schedule := this.GetSchedule();
    scheduletime := schedule.Time();
    
    latestends := POAValueSet::N( poa, 'LatestEnd', Real::MaxReal() );
    
    traverse( task.Scope().Operations(), Elements, element,
               this.GetOperation( element ).LatestEnd().IsFinite() )
    {
      operation_in_suboptimizer := this.GetOperation( element );
      end := operation_in_suboptimizer.LatestEnd().HoursAsReal( scheduletime );
      node := poa.Node( SuboptimizerMRSPOA::POANameNodeOperation( element ), element );
      latestends.Set( node, end );
    }
    
    const := poa.NewEndConstraint( SuboptimizerMRSPOA::NamePOALatestEndConstraint() );
    const.Expression( poa.Expression( SuboptimizerMRSPOA::NamePOATimeExpression() ) );
    const.ValueSet( latestends );
    const.SetEnd();
    
    const.Hard( false );
    const.ViolationOffset( guard( this.Strategy().MRS_KPIOffsetConstraintLatestEnd(), 0.0 ) );
    const.Factor( guard( this.Strategy().MRS_KPIWeightConstraintLatestEnd(), 0.0 ) );
    const.Power( OptimizerRun::GetPOAPower( guard( this.Strategy().MRS_KPIPowerConstraintLatestEnd(), OptimizerRun::POADefaultPower() ) ) );
  *]
}
