Quintiq file version 2.0
#parent: #root
StaticMethod AllowedMoveDown (CampaignTasks selection, output String feedback_o) declarative remote as Boolean
{
  TextBody:
  [*
    // gyg1 Jul-19-2016 (created)
    // JK 12-16-2016 QP 8382 Adjusted relational logic for updated object model with multiple campaigns
    
    lasttask := select( selection, Elements, element, isnull( element.NextCampaignTask() ));
    value    := isnull( lasttask );
    
    if( not value )
    {
      feedback_o := Translations::SC_SelectionAlreadyAtLastPosition( CampaignTask::GetIdentifier( lasttask ));
    }
    else
    {
      isallowedhard   := true;
      operations      := selectset( selection, Elements.OperationCampaign.Operation_SC, op, true );
      resource        := selection.Element( 0 ).Resource_SC();
      value           := Operation_SC::CanReplan( operations, false, feedback_o, isallowedhard );
    
      if( resource.istype( SingleResourceBase ) )
      {
        value := resource.astype( SingleResourceBase).AllowedScheduling( selection, //structured[CampaignTask]
                                                                     false, //skipsoftcheck
                                                                     false, //ctrlshiftkey
                                                                     true, //movedown
                                                                     feedback_o,
                                                                     isallowedhard ) and value;
      }
      else
      {
        value := resource.astype( BatchResource ).AllowedScheduling( selection,  //structured[CampaignTask]
                                                                     false, //skip soft check
                                                                     true, //move down
                                                                     feedback_o,
                                                                     isallowedhard ) and value;
      }
    }
    
    feedback_o := Translations::SC_CannotBeMovedDown( CampaignTask::GetSelectedObjects( selection ) )
                  + String::NewLine()
                  + feedback_o;
    
    return value;
  *]
}
