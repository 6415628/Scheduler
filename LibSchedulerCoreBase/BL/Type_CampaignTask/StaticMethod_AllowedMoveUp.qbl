Quintiq file version 2.0
#parent: #root
StaticMethod AllowedMoveUp (CampaignTasks selection, output String feedback_o) declarative remote as Boolean
{
  TextBody:
  [*
    // gyg1 Jul-19-2016 (created)
    // JK 12-16-2016 QP 8382 Adjusted relational logic for updated object model with multiple campaigns
    
    toptask := select( selection, Elements, element, isnull( element.PreviousCampaignTask() ) );
    value   := isnull( toptask );
    
    if( not value )
    {
      feedback_o := Translations::SC_SelectionAlreadyAtFirstPosition( CampaignTask::GetIdentifier( toptask ));
    }
    else
    {
      isallowedhard   := true;
      operations      := selectset( selection, Elements.OperationCampaign.Operation_SC, op, true );
      resource        := selection.Element( 0 ).Resource_SC();
      value           := Operation_SC::CanReplan( operations, false, feedback_o, isallowedhard );
    
      ontype( resource )
      {
        SingleResourceBase as sr:
        {
          value := sr.AllowedScheduling( selection, //structured[CampaignTask]
                                         false, //skipsoftcheck
                                         false, //ctrlshiftkey
                                         false, //movedown
                                         feedback_o,
                                         isallowedhard ) and value;
        }
        BatchResource as br:
        {
          value := br.AllowedScheduling( selection,  //structured[CampaignTask]
                                         false, //skip soft check
                                         false, //move down
                                         feedback_o,
                                         isallowedhard ) and value;
        }
      }
    }
    
    feedback_o := Translations::SC_CannotBeMovedUp( CampaignTask::GetSelectedObjects( selection ) )
                  + String::NewLine()
                  + feedback_o;
    
    return value;
  *]
}
