Quintiq file version 2.0
#parent: #root
Method CreateOrdersNVT () as owning NamedValueTree
{
  Description: 'Creates the Orders xml to Apriso in NVT format.'
  TextBody:
  [*
    // woi1 Feb-13-2017 (created)
    
    // create empty nvt message
    nvt := LibInt_MessageAccessor::CreateEmptyMessage();
    
    // Gets the body handle of NVT
    body_handle := nvt.GetHandle( Constants::NVT_Body() );
    // Gets the first node of body
    body_node := nvt.Root().Children( body_handle ).Element( 0 ); // select doesn't work here
    
    apriso_orders_handle := nvt.GetHandle( Constants::Apriso_ExportXMLEnvelopTag_Orders() );
    apriso_orders_node := body_node.AddChild( apriso_orders_handle );
    
    sorted_exportedpu := selectsortedset( this, FeedbackManager.ExportedSchedule.ExportedPlanUnit.astype( GenericExportedPlanUnit ), 
                                          exportedpu, true, exportedpu.WipOrderNo() ); // sorted to make it easier to read
    // Order
    traverse( sorted_exportedpu, Elements, exportedpu )
    {
      // creates the nvt for exported plan unit
      order_node := exportedpu.CreateOrdersNVT( nvt, apriso_orders_node );
    
      sorted_exportedtask := selectsortedset( exportedpu, ExportedSingleTask, extask, true, extask.WorkOrderOperationSequenceNr() ); // sorted to make it easier to read
      // Operation
      traverse( sorted_exportedtask, Elements, extask )
      {
        // creates the nvt for exported task under exported plan unit
        extask.CreateOrdersNVT( nvt, order_node );
      }
    }
    
    return &nvt;
  *]
}
