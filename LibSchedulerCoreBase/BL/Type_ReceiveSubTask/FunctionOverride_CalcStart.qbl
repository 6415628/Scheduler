Quintiq file version 2.0
#parent: #root
FunctionOverride CalcStart
{
  Description: 'Computes the start time of receive subtask.'
  TextBody:
  [*
    // alexandern 28-May-2015 QP # 5420
    // Kyle June 04, 2015 #5411 Use previous subtask's earliest end if it exists
    task  := this.Task_SC();
    start := task.Schedule().Time();
    prevsubtask := this.Previous();
    
    if( not isnull( prevsubtask ) )
    {
      // In default case where calendar downtime starts right on the previous subtask end,
      // this subtask will start after downtime end.
      start := task.Resource_SC().ResourceCalendar().NextAvailable( prevsubtask.End() );
    }
    else if( not isnull( task.Strategy_SC() ) )
    {
      // Kyle 27-Aug-2015 QP #5740
      // If receive subtask is the first subtask in the sequence,
      // ensure the start time is after both the transfer and receive resource's calendar downtime.
      start := this.GetNextFitStart( task.Strategy_SC().Start() );
    }
    
    start := maxvalue( start, this.SimulatedEarliestStart() );
    this.Start( start );
  *]
}
