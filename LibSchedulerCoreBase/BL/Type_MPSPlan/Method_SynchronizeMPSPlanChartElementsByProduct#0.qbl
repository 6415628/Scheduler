Quintiq file version 2.0
#parent: #root
Method SynchronizeMPSPlanChartElementsByProduct
{
  Description: 'Synchronize MPS planned order chart elements by product starting from the day after schedule date for the duration of MPS Plan Horizon.'
  TextBody:
  [*
    allInstances := selectset( this, MPSPlanChartElementByProduct, mpsPlanChartElementByProduct, true )
    updatedInstances := construct( MPSPlanChartElementByProducts );
    
    traverse( this, MPSPlannedOrder.Product_SC, product )
    {
      schedulerDate := this.Schedule().Time().Date();
      chartElementsCounter := 1;
      // We assume the periods are always 1 day size for now
      // We will revisit this when we change the synchronization of MPSChartElements to be based on a derived MPS period size from MP
      nrOfChartElementsNeeded := this.Schedule().IntegrationConfiguration().MPSPlanHorizon().Days();
      
      while( chartElementsCounter <= nrOfChartElementsNeeded )
      {
        dueDate := schedulerDate + Duration::Days( chartElementsCounter );
        mpsPlanChartElementByProduct := MPSPlanChartElementByProduct::FindMPSPlanChartElementByProduct( product.Id(), dueDate );
        
        if( isnull( mpsPlanChartElementByProduct ) )
        {
          mpsPlanChartElementByProduct := this.Schedule().ObjectFactory().CreateMPSPlanChartElementByProduct( this, product.Id(), dueDate );
        }
        
        updatedInstances.Add( mpsPlanChartElementByProduct );
        
        chartElementsCounter++;
      }
    }
    
    oldInstances := allInstances.Difference( updatedInstances );
    
    traverse( oldInstances, Elements, mpsPlanChartElementByProduct )
    {
      mpsPlanChartElementByProduct.Remove();
    }
  *]
}
