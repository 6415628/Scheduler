Quintiq file version 2.0
#parent: #root
Method SynchronizeExportedDemoDataPlannedFulfillment
{
  Description: 'Synchronize ExportedDemoDataPlannedFulfillment which will be exported as part of export demo data interface.'
  TextBody:
  [*
    exportedSchedule := this.Schedule().FeedbackManager().ExportedSchedule();
    allinstances := selectset( this, Schedule.FeedbackManager.ExportedSchedule.ExportedDemoDataPlannedFulfillment, exportedDemoDataPlannedFulfillment, true );
    updatedinstances := construct( ExportedDemoDataPlannedFulfillments );
    
    traverse( this, Schedule.Demand.Fulfillment, fulfillment, not fulfillment.IsFixedReservation() )
    {
      exportedDDPlannedFulfillment := ExportedDemoDataPlannedFulfillment::FindExportedDemoDataPlannedFulfillment( fulfillment.DemandId(), fulfillment.SupplyId() );
      
      if( isnull( exportedDDPlannedFulfillment ) )
      {
        exportedDDPlannedFulfillment := ExportedDemoDataPlannedFulfillment::Create( exportedSchedule, fulfillment.DemandId(), fulfillment.SupplyId() );
      }
      
      if( not isnull( exportedDDPlannedFulfillment ) )
      {
        exportedDDPlannedFulfillment.Update( fulfillment );
      }
      
      updatedinstances.Add( exportedDDPlannedFulfillment );
    }
    
    //Delete old instances
    oldinstances := allinstances.Difference( updatedinstances );
    
    traverse( oldinstances, Elements, exportedDDPlannedFulfillment )
    {
      exportedDDPlannedFulfillment.Remove();
    }
  *]
}
