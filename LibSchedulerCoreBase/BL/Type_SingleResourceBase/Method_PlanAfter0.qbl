Quintiq file version 2.0
#parent: #root
MethodOverride PlanAfter (Task_SCs tasks, Task_SC after)
{
  TextBody:
  [*
    // Get sorted tasks
    sortedtasks := Resource_SC::GetSortedTasksFromTasks( tasks );
    
    // Reverse sort so that we do not need a moving target
    sortedtasks.Reverse();
    
    // Plan the reversed sorted tasks
    operations := construct( Operation_SCs );
    // Perform 'chunking' during planning so that tasks with operations are planned in 1 go to maintain the original sequence
    traverse( sortedtasks, Elements, task )
    {
      // If there is no operation for the task, just plan the task after the "after" task
      if( isnull( task.GetOperation() ) )
      {
        // If there are operations collected in previous loop, plan the operations first
        if( operations.Size() > 0 )
        {
           this.PlanAfter( operations, after );
           // After plan the operations, just flush the structured set in order to collect new set of operations to be planned
           operations.Flush();
        } 
        // Plan the task without operation
        this.PlanAfter( task, after ); 
      }
      else
      {
        // Collect the operation to be planned
        operations.Add( task.GetOperation() ); 
      }
    }
    
    // If there are operations collected in traverse loop, plan the operations
    if( operations.Size() > 0 )
    {
       this.PlanAfter( operations, after );
    }
  *]
}
