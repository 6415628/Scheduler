Quintiq file version 2.0
#parent: #root
Method SynchronizeRuleScopeInShutdownType (ImportedRule importedRule)
{
  Description: 'Synchronize rule scope in shudown type'
  TextBody:
  [*
    // Get all current instances
    allInstances := selectset( this, RuleScopeInShutdownType, ele, true );
    updatedInstances := construct( RuleScopeInShutdownTypes );
    
    // Rule scope in shutdown type
    traverse( importedRule, ImportedRuleScopeInShutdownType, ele )
    {
      shutdowntype :=  ShutdownType::FindShutdownType( ele.ShutdownTypeID() );
      rsist := select( this, 
                       RuleScopeInShutdownType, 
                       rulescope, 
                       rulescope.ShutdownType().ShutdownTypeID() = ele.ShutdownTypeID() );
                                      
      if( not isnull( shutdowntype ) and isnull( rsist ) )
      {
        rsist := RuleScopeInShutdownType::Create( this );
        rsist.ShutdownType( relset, shutdowntype );
      }
      
      if( not isnull( rsist ) )
      {
        updatedInstances.Add( rsist ); 
      }
    }
    
    // Delete old instances
    oldInstances := allInstances.Difference( updatedInstances );
    traverse( oldInstances, Elements, e )
    {
      e.Remove(); 
    }
  *]
}
