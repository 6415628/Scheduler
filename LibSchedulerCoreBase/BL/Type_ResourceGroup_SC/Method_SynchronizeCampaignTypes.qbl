Quintiq file version 2.0
#parent: #root
Method SynchronizeCampaignTypes
{
  Description: 'Synchronizes ImportedCampaignType_SCs with CampaignType_SCs.'
  TextBody:
  [*
    // James Kwapien Dec-17-2016 QP #8389 Synchronize all campaign types from imported type
    
    allinstances := selectset( this, CampaignType, ct, true );
    updatedinstances := construct( CampaignTypes );
    importedrg := select( this.Schedule(), ImportedResourceGroup_SC, irg, irg.ResourceGroup() = this.Name() );
    
    if( not isnull( importedrg ))
    {
      traverse( importedrg, ImportedCampaignType_SC, ict )
      {
        // Eric Lowrie Dec-28-2016 QP #8381, cannot use name in the type index since that would require it to be read-only,
        //                                   and that would prevent users from editing it with this QP
        //campaigntype := CampaignType::FindCampaignType( ict.Name(), ict.ResourceGroup() );
        importedname := ict.Name();
        importedgroup := ict.ResourceGroup();
        campaigntype := select( this, CampaignType, ct, ct.Name() = importedname 
                                                        and ct.ResourceGroup() = importedgroup );
    
        if( isnull( campaigntype ) )
        {
          campaigntype := CampaignType::Create( this, ict.Name(), ict.DisplayIndex() );
        }
        campaigntype.Update( ict );
        
        updatedinstances.Add( campaigntype );
        
        campaigntype.SynchronizeCampaignInCampaignType( ict );
      }
    
      // Delete campaign types if they do not exist in data broker
      oldinstances := allinstances.Difference( updatedinstances );
    
      traverse( oldinstances, Elements, campaigntype )
      {
        campaigntype.Delete();
      }
    }
  *]
}
