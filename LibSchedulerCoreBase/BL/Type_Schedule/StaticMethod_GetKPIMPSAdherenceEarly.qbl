Quintiq file version 2.0
#parent: #root
StaticMethod GetKPIMPSAdherenceEarly (const OptimizerStrategyInSchedule optimizerstrategy, constcontent Operation_SCs operations) const declarative remote as Real
{
  Description: 'Returns the KPI quantity of operations which are scheduled before MPS scheduling window'
  TextBody:
  [*
    // Calculate the quantity of operations which are scheduled before MPS scheduling window
    // Only the following operations will be calculated
    // 1. MPS_WindowStart is finite
    // 2. Not dummy work order ( Checking logic in Operation.IsEligibleForKPICalculation )
    // 3. No feedback ( Checking logic in Operation.IsEligibleForKPICalculation )
    // 4. Not finished yet ( Checking logic in Operation.IsEligibleForKPICalculation )
    // 5. IsPlanned
    // 6. IsDueInSchedulingHorizon 
    // 7. Not adherence with MPS_WindowStart ( Start < MPS_WindowStart )
    
    value := 0.0;
    
    if( not isnull( optimizerstrategy ) )
    {
      a := optimizerstrategy.PenaltyParameterMPSAdherenceEarlySlope_a();
      b := optimizerstrategy.PenaltyParameterMPSAdherenceEarlyPower_b();
      c := optimizerstrategy.PenaltyParameterMPSAdherenceEarlyOffset_c();
      
      value := sum( operations,
                    Elements,
                    op,
                    op.MPS_WindowStart().IsFinite()
                    and op.IsEligibleForKPICalculation()
                    and op.IsPlanned()
                    and op.IsDueInSchedulingHorizon()
                    and not op.IsAdherenceCPPlannedStart(),
                    op.PlanUnit_SC().Quantity() 
                    * ( a * pow( op.GetMPSAdherenceEarlyDeviation() + c, b ) ) );
    }
    
    return value.Round( 2 );
  *]
}
