Quintiq file version 2.0
#parent: #root
StaticMethod GetKPIQuantityDueNotScheduled (constcontent Operation_SCs operations) const declarative remote as Real
{
  Description: 'Returns the KPI quantity of operations which are due but not scheduled on time or not scheduled yet'
  TextBody:
  [*
    // Calculate the quantity which due but not schedule yet
    // Only the following operations will be calculated
    // 1. Not dummy work order ( Checking logic in Operation.IsEligibleForKPICalculation )
    // 2. No feedback ( Checking logic in Operation.IsEligibleForKPICalculation )
    // 3. Not finished yet ( Checking logic in Operation.IsEligibleForKPICalculation )
    // 4. IsCPPlanDue or IsDueInSchedulingHorizon
    // 5. Not scheduled yet
    value := sum( operations,
                  Elements,
                  op,
                  op.IsEligibleForKPICalculation()
                  and ( op.IsCPPlanDue() or op.IsDueInSchedulingHorizon() )
                  and not op.IsPlanned(),
                  pow( op.Urgency(), 3 ) * op.PlanUnit_SC().Quantity() );
    
    return value.Round( 2 );
  *]
}
