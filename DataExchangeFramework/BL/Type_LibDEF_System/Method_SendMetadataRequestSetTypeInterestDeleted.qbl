Quintiq file version 2.0
#parent: #root
Method SendMetadataRequestSetTypeInterestDeleted (LibDEF_SetTypeInterestOfDataset datasetInterest_i, Boolean isDatasetEvent_i)
{
  Description: 'Send a Metadata message for requesting the SetTypeInterest to be deleted.'
  TextBody:
  [*
    nvt := LibDEF_DataAccessor::CreateMessageMetadata();
    
    // Don't delete the SetInterests that where explicitly created.
    setGUIDsExcludedFromDeletion := selectvalues( datasetInterest_i,
                                                  SetTypeMeta.SetMeta.SetInterestOfDataset,
                                                  setInterestOfDataset,
                                                  not setInterestOfDataset.IsCreatedFromSetTypeInterest(),
                                                  setInterestOfDataset.SetMeta().GUID() );
    
    // define Type and required Header
    LibDEF_DataAccessor::SetMetadataHeaderProperty( nvt, LibDEF_DataAccessor::HEADER_TYPE(),                         LibDEF_DataAccessor::TYPE_METADATA_SETTYPEINTERESTDELETED() );
    LibDEF_DataAccessor::SetMetadataHeaderProperty( nvt, LibDEF_DataAccessor::HEADER_SETTYPEGUID(),                  datasetInterest_i.SetTypeMeta().GUID()                      );
    LibDEF_DataAccessor::SetMetadataHeaderProperty( nvt, LibDEF_DataAccessor::HEADER_SETGUIDSEXCLUDEDFROMDELETION(), setGUIDsExcludedFromDeletion.ToString( "," )                );
    LibDEF_DataAccessor::SetMetadataHeaderProperty( nvt, LibDEF_DataAccessor::HEADER_DATASETMDSID(),                 datasetInterest_i.DatasetMDSID()                            );
    LibDEF_DataAccessor::SetMetadataHeaderProperty( nvt, LibDEF_DataAccessor::HEADER_DATASETDTD(),                   datasetInterest_i.DataTransformationDefinition()            );
    LibDEF_DataAccessor::SetMetadataHeaderProperty( nvt, LibDEF_DataAccessor::HEADER_ISDATASETEVENT(),               isDatasetEvent_i                                            );
    
    dataBroker := this.DataBroker();
    event      := dataBroker.EventCreate( this, false );
    
    LibDEF_Util::EventLog( event, "Sending SetTypeInterest deleted metadata request" );
    
    dataBroker.SendMessage( this, nvt, LibDEF_DataAccessor::KIND_REQUEST(), event );
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
