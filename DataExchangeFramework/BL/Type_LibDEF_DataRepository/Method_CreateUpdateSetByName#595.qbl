Quintiq file version 2.0
#parent: #root
Method CreateUpdateSetByName (String channelName_i, String setTypeName_i, String setName_i, 
  LibDEF_DataTransformation dataTransformation_i, String dataFilterArguments_i)
{
  Description: 'Create or update Set with SetObjects.'
  TextBody:
  [*
    // Get the Set.
    set := this.GetEmptySetByName( channelName_i, setTypeName_i, setName_i );
    
    // Get the data to be exchanged.
    data := ifexpr( dataFilterArguments_i.Length() = 0,
                    dataTransformation_i.CollectData(),
                    dataTransformation_i.CollectData( dataFilterArguments_i ) );
    
    if( not dataTransformation_i.AdvancedTransformationMode() )
    {
      // For simple transformation:
      // Transform each data-element into a SetObject.
      traverse( data, Elements, object )
      {
        setObject := set.CreateSetObject();
        dataTransformation_i.TransformDataToSetObject( object, setObject );
      }
    }
    else
    {
      // For advanced transformation with more flexibility:
      // The method must be overridden to create SetObject instances and transformation
      dataTransformation_i.AdvancedTransformDataAndAddToSet( set, data );
    }
    
    // update the Set timestamp
    set.UpdateTimestamp();
    
    // Add the set to the SetQueue, from where it will be synced to the DataBroker.
    if( isnull( set.SetQueueToBroker() ) )
    {
      set.SetType().SetQueue().AddSetToBroker( set );
    }
  *]
  InterfaceProperties { Accessibility: 'Module' }
}
